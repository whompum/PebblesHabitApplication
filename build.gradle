// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'dependencies.gradle'
    apply from: 'versioning.gradle'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.50'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.1")
}

detekt {
    toolVersion = "1.0.1"
    input = files("src/main/kotlin")
    filters = ".*/resources/.*,.*/build/.*"
    config = files("$projectDir/detektconfig.yml")
    parallel = true
    reports {
        xml {
            enabled = true
            destination = file("$buildDir/reports/detekt-report.xml")
        }
    }
    idea {
        path = ".idea/"
        codeStyleScheme = "codestyle.xml"
        report = "$project.projectDir/reports"
        mask = "*.kt,"
    }
}

subprojects {
    afterEvaluate { project ->

        if (project.hasProperty('android') && project.path != ':Base') {
            android {
                compileSdkVersion this.ext.compile_sdk_version
                buildToolsVersion this.ext.build_tools_version

                defaultConfig {
                    minSdkVersion this.ext.min_sdk_version
                    targetSdkVersion this.ext.target_sdk_version
                    versionCode this.ext.code
                    versionName "${this.ext.major}.${this.ext.minor}.${this.ext.subminor}"
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                buildTypes {

                    debug {
                        versionNameSuffix "-debug"
                        debuggable true
                        buildConfigField "boolean", "enableLogs", "true"
                        crunchPngs false
                        minifyEnabled false
                    }

                    release {
                        debuggable false
                        buildConfigField "boolean", "enableLogs", "false"
                        minifyEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }
            }
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
