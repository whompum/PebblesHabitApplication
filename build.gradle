// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.3.50'

    apply from: 'gradle-configurations/dependencies.gradle'
    apply from: 'gradle-configurations/versioning.gradle'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.50'
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion this.ext.compile_sdk_version
                buildToolsVersion this.ext.build_tools_version

                defaultConfig {

                    if (project.path == ':App')
                        applicationId = "com.example.pebbles_habbitapp"

                    minSdkVersion this.ext.min_sdk_version
                    targetSdkVersion this.ext.target_sdk_version
                    versionCode this.ext.code
                    versionName "${this.ext.major}.${this.ext.minor}.${this.ext.subminor}"
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

                    def fileProperties = new Properties()
                    fileProperties.load(new FileInputStream(file("adminCredentials.properties")))
                    buildConfigField "String", "ADMIN_USERNAME", fileProperties['AdminUsername']
                    buildConfigField "String", "ADMIN_PASSWORD", fileProperties['AdminPassword']
                    buildConfigField "boolean", "USE_ADMIN_ACCOUNT", "true"
                }

                buildTypes {

                    debug {
                        versionNameSuffix "-debug"
                        debuggable true
                        buildConfigField "boolean", "enableLogs", "true"
                        crunchPngs false
                        minifyEnabled false
                    }

                    release {
                        debuggable false
                        buildConfigField "boolean", "enableLogs", "false"
                        buildConfigField "boolean", "USE_ADMIN_ACCOUNT", "false"
                        minifyEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }

                testOptions {
                    unitTests {
                        includeAndroidResources = true
                    }
                }
            }
        }
    }
}

allprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply from: "$rootProject.rootDir/sca/detekt.gradle"
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
